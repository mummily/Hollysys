<?xml version="1.0" encoding="ISO-8859-1"?>
<pou>
	<path><![CDATA[\/HWAlgorithms]]></path>
	<name>FLOWCOMP</name>
	<secondName>流量补偿</secondName>
	<description></description>
	<flags>2048</flags>
	<auto-sort>0</auto-sort>
	<exporttime>2020-02-24 10:10:53</exporttime>
	<amendtime>2020-02-22 15:28:12</amendtime>
	<downloadtime></downloadtime>
	<modifier></modifier>
	<PouPaperSize>A4</PouPaperSize>
	<PouPrintType>1</PouPrintType>
	<PouExcuteOrder>0</PouExcuteOrder>
	<interface>
<![CDATA[FUNCTION_BLOCK FLOWCOMP
VAR
		RG(2118): REAL := 1;		(*设计的参考比重/分子量*)
		RP(2118): REAL := 1;		(*设计压力（绝压）*)
		RQ(2118): REAL := 1;		(*设计的水蒸气干度*)
		RT(2118): REAL := 1;		(*设计温度（绝对温度）*)
		P0(2118): REAL := 0.101325;		(*压力零点参考,与P的单位一致进行调整*)
		T0(2118): REAL := 273.15;		(*绝对温度转换因数*)
		RX(2118): REAL := 1;		(*参考蒸汽压缩系数*)
		C(2118): REAL := 1;		(*刻度因子*)
		C1(2118): REAL := 1;		(*校正常量1*)
		C2(2118): REAL := 1;		(*校正常量2*)
		COMPTERM(2118): REAL := 0;		(*补偿项*)
		PVEQN(1094): BYTE := 0;		(*补偿公式选择0-4*)
		PVCHAR(2118): BOOL := FALSE;		(*FALSE-Linear线性 TRUE-Sqrroot开方*)
		COMPLOLM(2118): REAL := 0.8;		(*补偿项低限*)
		COMPHILM(2118): REAL := 1.25;		(*补偿项高限*)
END_VAR
VAR_INPUT
		P(2119): REAL := 0;		(*测量的实际表压*)
		G(2119): REAL := 0;		(*测量或计算的比重/分子量*)
		Q(2119): REAL := 0;		(*测量的实际水蒸气的干度系数*)
		X(2119): REAL := 0;		(*测量的实际水蒸气的压缩系数*)
		T(2119): REAL := 0;		(*测量的实际摄氏温度*)
		F(2119): REAL := 0;		(*未补偿的测量流量*)
		FSTS(2119): WORD := 0;		(*测量流量的品质0-坏非0正常*)
		PSTS(2119): WORD := 0;		(*测量的实际表压品质0-坏非0正常*)
		GSTS(2119): WORD := 0;		(*测量或计算的比重/分子量品质0-坏非0正常*)
		QSTS(2119): WORD := 0;		(*测量的实际水蒸气的干度系数品质0-坏非0正常*)
		XSTS(2119): WORD := 0;		(*测量的实际水蒸气的压缩系数品质0-坏非0正常*)
		TSTS(2119): WORD := 0;		(*测量的实际摄氏温度品质0-坏非0正常*)
END_VAR
VAR_IN_OUT
END_VAR
VAR_OUTPUT
		PVCALC(2118): REAL := 0;		(*补偿后输出*)
		PVAUTOST(2118): WORD := 0;		(*输出状态0-好*)
END_VAR]]>
	</interface>
	<st>
		<body>
<![CDATA[;
(*0-EQA：用于液体的质量流量或体积流量的补偿;实际流量、测量或计算的比重做为补偿输入;
   1-EQB：用于气体或蒸汽的质量流量补偿。实际流量、实际摄氏温度、实际表压为补偿输入;
   2-EQC：用于气体或蒸汽的质量流量补偿。实际流量、测量或计算的比重、实际摄氏温度、表压为补偿输入;
   3-EQD：用于气体或蒸汽的体积流量补偿。实际流量、实际摄氏温度、表压、测量或计算的分子量为补偿输入;
   4-EQE：用于水蒸汽的质量流量补偿。实际流量、实际摄氏温度、表压、蒸汽压缩系数（X）和蒸汽干度系数（Q）做为补偿输入。也可用于气体或液体的交接计量。
	根据补偿公式计算的补偿项COMPTERM限定在COMPHILM和COMPLOLM之间。
	输出质量由所使用的各输入信号的质量决定。

版本： V0.0

*)
IF COMPHILM<COMPLOLM THEN
	COMPHILM:=1.25;
END_IF
IF COMPLOLM>COMPHILM THEN
	COMPLOLM:=0.8;
END_IF
IF C2=0 THEN
	C2:=1;
END_IF;
CASE PVEQN OF
	0:
		COMPTERM:=DIV0(G,RG);
		PVAUTOST:=FSTS OR GSTS ;
	1:
		COMPTERM:=DIV0((P+P0)*RT,(RP*(T+T0)));
		PVAUTOST:=FSTS OR PSTS OR TSTS;
	2:
		COMPTERM:=DIV0((P+P0)*RT*G,RP*(T+T0)*RG);
		PVAUTOST:=FSTS OR PSTS OR GSTS OR TSTS;
	3:
		COMPTERM:=DIV0((P+P0)*RT*RG,RP*(T+T0)*G);
		PVAUTOST:=FSTS OR PSTS OR GSTS OR TSTS;
	4:
		COMPTERM:=DIV0((P+P0)*RT*X*RQ,RP*(T+T0)*RX*Q);
		PVAUTOST:=FSTS OR PSTS OR GSTS OR QSTS OR XSTS OR TSTS;
END_CASE
IF PVCHAR THEN
	COMPTERM:=LREAL_TO_REAL(SQRT0(COMPTERM));
END_IF
COMPTERM:=MAX(MIN(COMPTERM,COMPHILM),COMPLOLM);
PVCALC:=C*DIV0(C1,C2)*F*COMPTERM;

]]>
		</body>
	</st>
</pou>
